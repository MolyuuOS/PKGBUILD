# Maintainer: bigsaltyfishes <bigsaltyfishes@gmail.com>

pkgname=gamescope-session-steam-git
_gitdir=gamescope-session-steam
pkgver=r6.3abe486
pkgrel=1
pkgdesc="Steam Big Picture session based on gamescope for MOlyuuOS"
arch=('any')
url="https://github.com/ChimeraOS/gamescope-session-steam"
license=('MIT')
groups=()
depends=('gamescope' 'gamescope-session-git')
optdepends=('chimera: for steam-tweaks integration')
makedepends=('git')
source=("${_gitdir}::git+https://github.com/ChimeraOS/${_gitdir}.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${_gitdir}"

# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$srcdir/${_gitdir}"

	# Some of the files are not needed and are not packaged to avoid conflicts
	install -Dm755 ${srcdir}/${_gitdir}/usr/bin/steam-http-loader ${pkgdir}/usr/bin/steam-http-loader
	cp -rv ${srcdir}/${_gitdir}/usr/share ${pkgdir}/usr/share
	rm -rf ${pkgdir}/usr/share/polkit-1
	install -Dm644 ${srcdir}/${_gitdir}/LICENSE ${pkgdir}/usr/share/licenses/${_gitdir}/LICENSE

	# font workaround for initial big picture mode startup
	mkdir -p ${pkgdir}/usr/share/fonts/truetype/ttf-dejavu
	ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/fonts/truetype/ttf-dejavu/DejaVuSans.ttf
}